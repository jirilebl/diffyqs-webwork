# Mechanical vibrations

## Institution(Fort Lewis College)
## Author(Paul Pearson)
## KEYWORDS('differential equations','mechanical vibrations')


##############################
#  Initialization

DOCUMENT(); 

loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "AnswerFormatHelp.pl",
  "PGgraphmacros.pl",
  "unionTables.pl",
  "answerHints.pl",
  "PGcourse.pl"
);

TEXT(beginproblem());

$refreshCachedImages = 1;


#############################
#  Setup

Context("Numeric")->variables->are(t=>'Real');




######################################
#  Begin graph canvas setup
#

$htmlsize = 300;
$texsize  = 400;

$gr = init_graph(-1,-4,7,4,axes=>[0,0],size=>[$htmlsize,$htmlsize]);
$gr->lb('reset');
foreach my $i (1,2,3,4,5,6) {
  $gr->moveTo($i,-4);
  $gr->lineTo($i,4,'gray');
  $gr->lb( new Label( $i,-0.25, $i,'black','center','middle'));
}
foreach my $i (1,2,3) {
  $gr->moveTo(-1,$i);
  $gr->lineTo(7,$i,'gray');
  $gr->moveTo(-1,-$i);
  $gr->lineTo(7,-$i,'gray');
  $gr->lb( new Label(-0.25, $i, $i,'black','right','middle'));
  $gr->lb( new Label(-0.25,-$i,-$i,'black','right','middle'));
}
$gr->lb( new Label(6.5,0.2,'t','black','center','middle'));
$gr->lb( new Label(0.2,3.75,'x','black','center','middle'));

#  End graph canvas setup
######################################


$x1 = list_random(0.25,0.75);
$x2 = $x1 + list_random(0.5,1);

$y1 = 0;
$y2 = non_zero_random(-4,4,1);

$T = Real("4*($x2-$x1)"); # period
$f = Compute("1 / $T"); # frequency
$omega = Compute("2 pi / $T"); # angular freq.
$gamma = Compute("2 pi $x2 / $T"); # phase shift
#$y = Compute("$y2/100 cos($omega t - $gamma)");
#$y_cm = Compute("$y2 cos($omega t - $gamma)");
#apparently plugging in the $omega and $gamma here will introduce
#a roundoff error and in some rare cases,
#correct answers might be marked as wrong
$y = Compute("$y2/100 cos((2 pi / $T) t - (2 pi $x2 / $T))");
$y_cm = Compute("$y2 cos((2 pi / $T) t - (2 pi $x2 / $T))");
$y0 = $y->eval(t=>0);
$yp0 = $y->D('t')->eval(t=>0);

if ($y2>0) {
  $type = "maximum";
} else {
  $type = "minimum";
}

$fn = new Fun ( $y_cm->perlFunction, $gr );
$fn->domain(-1,7);
$fn->steps(150);
$fn->color('blue');

Context()->functions->undefine("sin");


#############################
#  Main text

Context()->texStrings;
BEGIN_TEXT
The graph shows the displacement from equilibrium of 
a mass-spring system as a function of time after the
vertically hanging system was set in motion at time
\( t = 0 \).
$PAR
\{ image(insertGraph($gr),width=>$htmlsize,height=>$htmlsize,tex_size=>$texsize) \}
$PAR
Assume that the units of time are 
seconds, and the units of displacement are centimeters.
The first \( t \)-intercept is \( ($x1,$y1) \) and the
first $type has coordinates \( ($x2,$y2) \).
$PAR
What is the period \( T \) of the periodic 
motion?
$BR
\( T = \) \{ ans_rule(15) \} seconds
$SPACE \{ AnswerFormatHelp("numbers") \}
$PAR
What is the frequency \( f \) in Hertz?
What is the angular frequency \( \omega \)
in radians / second?
$BR
\( f = \) \{ ans_rule(15) \} Hertz
$SPACE \{ AnswerFormatHelp("numbers") \}
$BR
\( \omega = \) \{ ans_rule(15) \} radians / second
$SPACE \{ AnswerFormatHelp("numbers") \}
$PAR
Determine the amplitude \( A \) and the 
phase angle \( \gamma \) (in radians), and express 
the displacement in the form 
\( x(t) = A \cos(\omega t - \gamma) \), with 
\( x \) in meters.
$BR
\( x(t) = {}\) \{ ans_rule(30) \} meters
$SPACE \{ AnswerFormatHelp("formulas") \}
$PAR
With what initial displacement \( y(0) \) and
initial velocity \( x^{\prime}(0) \) was the system set 
into motion?
$BR
\( x(0) = {}\) \{ ans_rule(10) \} meters
$SPACE \{ AnswerFormatHelp("numbers") \}
$BR
\( x^{\prime}(0) = {}\) \{ ans_rule(10) \} meters / second
$SPACE \{ AnswerFormatHelp("numbers") \}
END_TEXT
Context()->normalStrings;


############################
#  Answers

$showPartialCorrectAnswers = 1;

ANS( $T->cmp() );
ANS( $f->cmp() );
ANS( $omega->cmp() );
ANS( $y->cmp()->withPostFilter(AnswerHints( 
  $y_cm => "Check your units",
)) );
ANS( $y0->cmp() );
ANS( $yp0->cmp() );



ENDDOCUMENT();
