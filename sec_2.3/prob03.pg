##DESCRIPTION
## Third order ODE with characteristic polynomial (r-1)(r^2 + a), general solution
##ENDDESCRIPTION

## DBsubject(Differential equations)
## DBchapter(Higher order differential equations)
## DBsection(Linear, constant coefficients, homogeneous (complex roots))
## Institution(Fort Lewis College, Oklahoma State University)
## Author(Paul Pearson, Jiri Lebl)
## Level(4)
## TitleText1('Notes on Diffy Qs: Differential Equations for Engineers')
## AuthorText1('Jiri Lebl')
## EditionText1('6')
## Section1('2.3')
## Problem1('')
## MO(1)
## KEYWORDS('ODE', 'linear', 'differential equation', 'third order', 'homogeneous', 'constant coefficients')


##############################
#  Initialization

DOCUMENT(); 

loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "AnswerFormatHelp.pl",
  "parserAssignment.pl",
  "PGcourse.pl"
);

TEXT(beginproblem());


#############################
#  Setup

Context("Numeric");
Context()->variables->add(
'c1'=>"Real",'c2'=>"Real",'c3'=>"Real",y=>"Real",
);
Context()->variables->set(
'c1'=>{limits=>[2,4]},
'c2'=>{limits=>[2,4]},
'c3'=>{limits=>[2,4]}
);
Context()->flags->set(
    formatStudentAnswer=>'parsed',
    reduceConstants=>0,
    reduceConstantFunctions=>0,
);
parser::Assignment->Allow;

$a  = list_random(2,3,5,6,7,8);

# char poly (r-1)(r^2 + $a)

$diffeq = "y^{\prime\prime\prime} - y^{\prime\prime} + $a y^{\prime} - $a y = 0"; # tex
$chareq = "r^3 - r^2 + $a r - $a = 0"; # tex

$answer = Compute("y = c1 e^x + c2 cos(sqrt($a) x) + c3 sin(sqrt($a) x)");


######################
#  Main text

Context()->texStrings;
BEGIN_TEXT
Find the general solution to
\[
$diffeq .
\]
In your answer, use \( c_1, c_2, \) and \( c_3 \) to 
denote arbitrary constants and \( x \) 
the independent variable.  Enter 
\( c_1 \) as ${LQ}c1${RQ}, 
\( c_2 \) as ${LQ}c2${RQ}, and 
\( c_3 \) as ${LQ}c3${RQ}.  Enter the solution as an equation \(y = ?\).
$PAR
\{ ans_rule(50) \}
\{ AnswerFormatHelp("equations") \}

$PAR
$BITALIC $BBOLD Book: $EBOLD
\{ htmlLink("https://www.jirka.org/diffyqs/html/sec_hol.html", "Section 2.3 of Notes on Diffy Qs") \}
$EITALIC
END_TEXT
Context()->normalStrings;


######################
#  Answer evaluation

$showPartialCorrectAnswers = 1;


ANS( $answer->cmp( checker => sub {

    my ( $correct, $student, $answerHash ) = @_;
    my $stu   = Formula($student->{tree}{rop});

    #################################
    #  Check for arbitrary constants
    #
    Value->Error("Is your answer the most general solution?") 
    if ( 
      Formula($stu->D('c1'))==Formula(0) || 
      Formula($stu->D('c2'))==Formula(0) || 
      Formula($stu->D('c3'))==Formula(0) 
    );


    ##################################
    #  Linear independence (Wronskian)
    #
    my $x = Real(1.43);

    my $a11 = $stu->eval('c1'=>1,'c2'=>0,'c3'=>0,x=>$x,y=>0);
    my $a12 = $stu->eval('c1'=>0,'c2'=>1,'c3'=>0,x=>$x,y=>0);
    my $a13 = $stu->eval('c1'=>0,'c2'=>0,'c3'=>1,x=>$x,y=>0);

    my $a21 = $stu->D('x')->eval('c1'=>1,'c2'=>0,'c3'=>0,x=>$x,y=>0);
    my $a22 = $stu->D('x')->eval('c1'=>0,'c2'=>1,'c3'=>0,x=>$x,y=>0);
    my $a23 = $stu->D('x')->eval('c1'=>0,'c2'=>0,'c3'=>1,x=>$x,y=>0);

    my $a31 = $stu->D('x','x')->eval('c1'=>1,'c2'=>0,'c3'=>0,x=>$x,y=>0);
    my $a32 = $stu->D('x','x')->eval('c1'=>0,'c2'=>1,'c3'=>0,x=>$x,y=>0);
    my $a33 = $stu->D('x','x')->eval('c1'=>0,'c2'=>0,'c3'=>1,x=>$x,y=>0);

    # my $wronskian =  
    #    $a11*($a22*$a33-$a32*$a23) -
    #    $a12*($a21*$a33-$a31*$a23) +
    #    $a13*($a21*$a32-$a31*$a22);
    # Value->Error("Your functions are not linearly independent or your answer is not complete") 
    # if ($wronskian==Real(0));

    Value->Error("Your functions are not linearly independent or your answer is not complete") 
    if (($a11*($a22*$a33-$a32*$a23)+$a13*($a21*$a32-$a31*$a22)) == ($a12*($a21*$a33-$a31*$a23)));


    #################################
    #  Check that the student answer is a solution to the DE
    #
    my $stu1 = Formula($stu->D('x'));
    my $stu2 = Formula($stu->D('x','x'));
    my $stu3 = Formula($stu->D('x','x','x'));
    return ($stu3 + $a * $stu1) == ($stu2 + $a * $stu);

    # my $stuDE = Formula($stuxxx - $stuxx + $a*$stux - $a*$stu)
    # ->with(test_points=>[[1,1,2,0.1,0],[2,1,1,0,0],[1,2,-1,-0.1,0],[-2,1,1,0,1]]);
    # return ($stuDE==Formula(0));


}));

##############################
#  Solution

Context()->texStrings;
BEGIN_SOLUTION
The characteristic equation is \( $chareq . \)
By trial and error (try simple \(r\)), we find that \(r=1\)
is a solution.  We divide the characteristic polynomial by \((r-1)\) and find that 
the characteristic equation can be simplified to
\( (r-1)(r^2+$a) = 0 .\)
The roots are therefore \(1\) and \(\pm \sqrt{$a} \, i. \)
We get three linearly independent solutions, \(y=e^x,\) \(y=\cos(\sqrt{$a} \, x),\) and \(y=\sin(\sqrt{$a} \, x).\)
$PAR
The general solution therefore is
\[ $answer . \]
END_SOLUTION
Context()->normalStrings;

################################################

COMMENT("Third order ODE with characteristic polynomial (r-1)(r^2 + a), general solution");

ENDDOCUMENT();
