# Higher order ODEs: constant coefficient second order homogeneous linear ODEs
# Linear, constant coefficients, homogeneous (complex roots)

## Institution(Fort Lewis College)
## Author(Paul Pearson)
## KEYWORDS('differential equations','second order linear ODE','constant coefficients')


##############################
#  Initialization

DOCUMENT(); 

loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "AnswerFormatHelp.pl",
  "parserAssignment.pl",
  "PGcourse.pl"
);

TEXT(beginproblem());


#############################
#  Setup

Context("Numeric");
Context()->variables->add(
  'c1'=>["Real",TeX=>'c_1',limits=>[2,4]],
  'c2'=>["Real",TeX=>'c_2',limits=>[2,4]],
  y=>"Real"
);
Context()->flags->set(
    formatStudentAnswer=>'parsed',
    reduceConstants=>0,
    reduceConstantFunctions=>0,
);
parser::Assignment->Allow;

$a  = random(2,6,1);
$aa = $a*$a;
$b = random(2,3,1);

$c2 = $b;
$c0 = $b * $aa;

# char poly $b( r^2+$a^2 )

$diffeq = "$c2 y^{\prime\prime} + $c0 y = 0"; # tex

$chareq = "$c2 r^2 + $c0 = 0"; # tex

$answer = Compute("y = c1 cos($a x) + c2 sin($a x)");


######################
#  Main text

Context()->texStrings;
BEGIN_TEXT
Find the general solution to \( $diffeq \).
Give your answer as \( y = ? \).  In your answer, use \( c_1 \) and \( c_2 \) to 
denote arbitrary constants and \( x \) 
the independent variable.  Enter \( c_1 \) as 
${LQ}c1${RQ} and \( c_2 \) as ${LQ}c2${RQ}.
$PAR
\{ ans_rule(40) \}
\{ AnswerFormatHelp("equations") \}
END_TEXT
Context()->normalStrings;


######################
#  Answer evaluation

$showPartialCorrectAnswers = 1;

ANS( $answer->cmp( checker => sub {
    my ( $correct, $student, $answerHash ) = @_;
    my $stu   = Formula($student->{tree}{rop});

    ################################
    #  Check for arbitrary constants
    #
    Value->Error("Is your answer the most general solution?") 
    if ( 
      Formula($stu->D('c1'))==Formula(0) || 
      Formula($stu->D('c2'))==Formula(0) 
    );

    ############################################
    #  Check for linear independence (Wronskian)
    #
    my $x = Real(1.24);

    my $a11 = $stu->eval('c1'=>1,'c2'=>0,x=>$x,y=>0);
    my $a12 = $stu->eval('c1'=>0,'c2'=>1,x=>$x,y=>0);
 
    my $a21 = $stu->D('x')->eval('c1'=>1,'c2'=>0,x=>$x,y=>0);
    my $a22 = $stu->D('x')->eval('c1'=>0,'c2'=>1,x=>$x,y=>0);

    # my $wronskian = $a11*$a22 - $a21*$a12;
    # Value->Error("Your functions are not linearly independent or your answer is not complete") 
    # if ($wronskian==Real(0));

    Value->Error("Your functions are not linearly independent or your answer is not complete") 
    if ($a11 * $a22 == $a21 * $a12);

    ########################################################
    #  Check that the student answer is a solution to the DE
    #
    my $stu1 = Formula($stu->D('x'));
    my $stu2 = Formula($stu->D('x','x'));
    return ($c2 * $stu2) == (-1 * $c0 * $stu);

    # my $stuDE = Formula($c2*$stuxx + $c0*$stu)
    # ->with(test_points=>[[1,1,0.1,0],[2,1,0,0],[1,2,-0.1,0],[1,1,0,1]]);
    # return ($stuDE==Formula(0));


}));

##############################
#  Solution

Context()->texStrings;
BEGIN_SOLUTION
The characteristic equation is \( $chareq ,\)
or \( $b ( r^2 + $a^2 ) =0 . \)
In other words we get \( r=\pm $a i .\)
We get two independent solutions, \(y=\cos( $a x) \) and \(y=\sin($a x) .\)
$PAR
The general solution therefore is
\[ $answer . \]
END_SOLUTION
Context()->normalStrings;

##############################

COMMENT("Characteristic polynomial b( r^2 + a^2 ).");

ENDDOCUMENT();
