# Fourier Series:  Sine series

## Institution(METU-NCC)
## Author(Benjamin Walter)

# prob02, prob03, prob04 are essentially the same, except
# for the picking of the $trig below.  They should be
# modified together.

#########################################################

DOCUMENT(); 

loadMacros(
  "PGstandard.pl",
  "MathObjects.pl",
  "AnswerFormatHelp.pl",
  "PGgraphmacros.pl",
  "answerHints.pl",
  "PGcourse.pl"
);

TEXT(beginproblem());
$showPartialCorrectAnswers = 1;

######################################################
##
##  Sine or Cosine series ?
#$trig = random(0,1,1) ? "cosine" : "sine";

$trig = "sine";

#######################################################
##  Setup Contexts
##
Context("Numeric");
Context()->variables->are();
$Num = Context()->copy;

Context()->variables->are(
  "n"=>["Real",limits=>[1,6],resolution=>1]);
$F_n = Context()->copy;

Context()->variables->are(
  "t"=>"Real");
$f_t = Context()->copy;


########################################################
##
##  Choose function

$L = random(3,8,1);    # interval length
$k = random(2,$L-1,1); # length of nonzero graph

my $m = random(-1,1,2);   # sign of nonzero graph  
my $sh = random(0,1,1);   # ? phase shift graph by L ?
my $rev = random(0,1,1);  # ? reverse graph ? 

$mid = ($sh==0) ? $k : $L-$k;  # where to switch f1 to f2

$f1 = ($sh==1)  ? Formula("0") :          
      ($rev==0) ? Formula("$m ($mid - t)") : 
                  Formula("$m t"); 
$f2 = ($sh==0)  ? Formula("0") :          
      ($rev==0) ? Formula("$m (t - $mid)") : 
                  Formula("$m ($L - t)"); 

$f1 = $f1->reduce;
$f2 = $f2->reduce;

$le1 = ($sh==0) ? "\le" : "<";
$le2 = ($sh==1) ? "\le" : "<";

######################################################
##
## points for evaluating the Fourier series
## and values of Fourier series

@x = (($rev==0) ? $L : $mid,
       ($sh==0) ? random(-$k+1,     -1,1)  : 
                  random(-$L+1,-$mid-1,1),
       ($sh==1) ? random( $L+1,$L+$k-1,1)  : 
                  random( 2*$L-$mid+1,2*$L-1,1)
     );


@F = (($rev==0) ? $f2->eval(t=>"$x[0]") : 
                  Real("$m*$k/2"),
       ($sh==0) ? $f1->eval(t=>"-$x[1]") :
                  $f2->eval(t=>"-$x[1]"),
       ($sh==0) ? $f1->eval(t=>"2*$L-$x[2]") :
                  $f2->eval(t=>"2*$L-$x[2]")
     );

if ($trig eq "sine") {   # sine series are odd-periodic
 $F[0] = ($rev==0) ? Real("0") : Real("$m*$k/2");
 $F[1] = -$F[1];      $F[2] = -$F[2];
}


########################################################
##
## Setup Graph
$refreshCachedImages=1;

$xmin = -($L+1); $xmax = $L+1;
$ymin = -($k+1); $ymax = $k+1;

$pixelwidth = 400;
$pixelheight = 400*($k+1)/($L+1);

$gr = init_graph(
          $xmin, $ymin,            # xmin, ymin
          $xmax, $ymax,            # xmax, ymax 
          'axes'=>[0,0],           # axis through origin 
          'grid'=>[2*$L+2,2*$k+2], # number of grid lines
          'size'=>[$pixelwidth,$pixelheight]);  # dimensions in pixels

add_functions($gr, 
  "$f1 for t in [0,$mid> using color:blue and weight:4",
  "$f2 for t in [$mid,$L] using color:blue and weight:4");

########################################################
##
## Record sine & cosine coefficients 
##
## reversing series is equivalent to 
##    f |--> k*u_{mid} - f
##
## shifting cosine series multiplies terms by (-1)^n
## shifting sine series multiplies terms by -(-1)^n


$A0 = Formula($F_n, "$m $k^2 / $L")->reduce;
$An = Formula($F_n, "(-1)^($rev)*2*$m*$L/(n*pi)^2(1 - cos(n*$k*pi/$L)) + $rev*(2*$m*$k/(n*pi)*sin(n*$k*pi/$L))")->reduce;
if ($sh==1) {  $An = Formula($F_n,"(-1)^n*$An");  }  

$Bn = Formula($F_n, "2*$m/(n*pi)*((1-$rev)*$k + (-1)^($rev+1)*$L/(n*pi)*sin(n*$k*pi/$L) - $rev*$k*cos(n*$k*pi/$L))")->reduce;
if ($sh==1) {  $Bn = Formula($F_n,"-(-1)^n*$Bn");  }  

########################################################
##
## Sine / Cosine coefficient entries

Context()->texStrings;

if ($trig eq "cosine") {
  $Coeff_entry = 
     "\(a_0 = \)" . ans_rule(10) . AnswerFormatHelp("numbers") .
     "${PAR}" .
     "\(a_n = \)" . ans_rule(60) . AnswerFormatHelp("formulas");
  $Series_formula =
     "\(\displaystyle C(t) = " .
     "\frac{a_0}{2} \, + \, \sum_{n=1}^\infty a_n\,\cos\left(\frac{n\pi}{$L}\,t\right)\)"; 
  $Series = "C";
} else {
  $Coeff_entry = 
     "\(b_n = \)" . ans_rule(60) . AnswerFormatHelp("formulas"); 
  $Series_formula =
     "\(\displaystyle S(t) = " .
     "\sum_{n=1}^\infty b_n\,\sin\left(\frac{n\pi}{$L}\,t\right)\)";
  $Series = "S";
}

Context()->normalStrings;

########################################################
##
##  Main text

Context()->texStrings;
BEGIN_TEXT

$SPACE $SPACE
\{ image( insertGraph($gr), 
width=>$pixelwidth*0.75,height=>$pixelheight*0.75,tex_size=>500 ) \}

$PAR

Let 
\(\displaystyle 
  f(t) = \begin{cases} 
            $f1\quad & \text{for}\ \   0    <  t $le1 $mid, \\
            $f2\quad & \text{for}\ \ $mid $le2 t   <  $L.
         \end{cases}\)

$PAR

Consider the Fourier ${BBOLD}$trig${EBOLD} series $Series_formula.

$PAR

Compute the Fourier $trig coefficients for \(f(t)\).

$PAR
  
$Coeff_entry

$PAR

Give values for the Fourier $trig series $Series(t).

$PAR
\(\phantom{-}$Series($x[0]) = \) \{ ans_rule(10) \}
\{ AnswerFormatHelp("numbers") \}

$PAR
\($Series($x[1]) = \) \{ ans_rule(10) \}
\{ AnswerFormatHelp("numbers") \}

$PAR
\(\phantom{-}$Series($x[2]) = \) \{ ans_rule(10) \}
\{ AnswerFormatHelp("numbers") \}

END_TEXT
Context()->normalStrings;


##############################
#  Answer evaluation

#    check coefficients
if ($trig eq "cosine") {  # check cosine coefficients
  ANS( $A0->cmp( 
         checker=>sub {
           my ($correct,$student,$ansHash ) = @_;
           if (2*$student==$correct) {
             Value::Error("Multiply by 2.${BR}"); 
           }
           return $student==$correct;
         } 
       ) 
     );
  ANS( $An->cmp() );
} else {         # check sine coefficients
  ANS( $Bn->cmp() );
}

#    check values of series
ANS( $F[0]->cmp() );
ANS( $F[1]->cmp() );
ANS( $F[2]->cmp() );

COMMENT("${BBOLD}Sine${EBOLD} series.${BR}Randomly picks one of four basic graph shapes (zero in first or second half; nonzero part cont on left or right).");

ENDDOCUMENT();
